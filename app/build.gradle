apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.cesar.androidtest"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file("release.keystore")
            keyAlias "release"
            storePassword "notMyRealPassword"
            keyPassword "notMyRealPassword"
        }
    }
    buildTypes {
        release {
            debuggable true
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

ext {
    retrofit_version = '2.4.0'
    dagger_version = '2.15'
    picasso_version = '2.71828'
    support_version = '27.1.0'
    espresso_version = '3.0.1'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Added by Android Studio
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-jackson:$retrofit_version"

    // Picasso
    implementation "com.squareup.picasso:picasso:$picasso_version"

    // Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    compileOnly 'javax.annotation:jsr250-api:1.0'

    // RecyclerView
    implementation "com.android.support:recyclerview-v7:$support_version"
    // CardView
    implementation "com.android.support:cardview-v7:$support_version"
    // Snackbar
    implementation "com.android.support:design:$support_version"
    // Chrome Custom Tabs
    implementation "com.android.support:customtabs:$support_version"

    // Tests ----------------

    // Instrumented Tests ---

    // Core
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-web:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$espresso_version"

    // Unit Tests -----------

    // Mockito
    testImplementation 'org.mockito:mockito-core:2.16.0'
    testImplementation 'com.nhaarman:mockito-kotlin:1.5.0'

    // Robolectric
    testImplementation "org.robolectric:robolectric:3.8"

    // Dagger
    kaptTest "com.google.dagger:dagger-compiler:$dagger_version"
    kaptTest "com.google.dagger:dagger-android-processor:$dagger_version"
}

kapt {
    generateStubs = true
}

android.applicationVariants.all { variant ->
    if (variant.buildType.name == "debug") {
        def aptOutputDir = new File(buildDir, "generated/source/apt/${variant.unitTestVariant.dirName}")
        variant.unitTestVariant.addJavaSourceFoldersToModel(aptOutputDir)
        assembleDebug.finalizedBy('assembleDebugUnitTest')
    }
}